#define VOXELCENTEROFFSET 0.5
#define WORLDSCALE 1.0
#define NUM_TEX_PER_ROW 4
#define TEX_WIDTH_NORMALIZED (1.0 / NUM_TEX_PER_ROW)

void voxeltexture_vp(
	float4 inPosition			: POSITION,
	float4 inColor				: COLOR,

	out float4 outPosition  	: POSITION,
	out float4 outWorldPosition	: TEXCOORD0,
	out float4 outColor			: TEXCOORD1,

	uniform float4x4 world,
	uniform float4x4 viewProj)
{
	outWorldPosition = mul(world, inPosition);
	
	outPosition = mul(viewProj, outWorldPosition);
	
	outColor = inColor;
}

void voxeltexture_fp(
	float4 inWorldPosition    	: TEXCOORD0,
	float4 inColor				: TEXCOORD1,

	out float4 outColor			: COLOR,

	uniform sampler2D texture)
{
	float3 worldNormal = cross(ddy(inWorldPosition.xyz / WORLDSCALE), ddx(inWorldPosition.xyz / WORLDSCALE));
	worldNormal = normalize(worldNormal);
	
	float2 pos;
	//Right
    if(worldNormal.x > 0.5)
    {
		pos = frac(inWorldPosition.zy + VOXELCENTEROFFSET); //Only divide by WORLDSCALE if it isn't a whole number
        pos.x = 1 - pos.x;
		pos.y = 1 - pos.y;
    }
	//Left
	if(worldNormal.x < -0.5)
    {
		pos = frac(inWorldPosition.zy + VOXELCENTEROFFSET);
		pos.y = 1 - pos.y;
    }
	//Top
    if(worldNormal.y > 0.5)
    {
		pos = frac(inWorldPosition.xz + VOXELCENTEROFFSET);
    }
	//Bottom
    if(worldNormal.y < -0.5)
    {
		pos = frac(inWorldPosition.xz + VOXELCENTEROFFSET);
		pos.y = 1 - pos.y;
    }
	//Front
    if(worldNormal.z > 0.5)
    {
		pos = frac(inWorldPosition.xy + VOXELCENTEROFFSET);
		pos.y = 1 - pos.y;
    }
	//Back
    if(worldNormal.z < -0.5)
    {
		pos = frac(inWorldPosition.xy + VOXELCENTEROFFSET);
        pos.x = 1 - pos.x;
		pos.y = 1 - pos.y;
    }
	
	//Texture atlas handling
	pos *= TEX_WIDTH_NORMALIZED;
	if(inColor.x != 0)
	{
		pos.x += inColor.x;
	}
	if(inColor.y != 0)
	{
		pos.y += inColor.y;
	}
	
	outColor = tex2D(texture, pos);
}